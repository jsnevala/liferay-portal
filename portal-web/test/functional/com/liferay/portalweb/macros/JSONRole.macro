definition {

	macro _addRole {

		if (isSet(roleDescription)) {
			var roleDescriptionEncoded = URLUtil.encode("${roleDescription}", "UTF-8");

			var roleDescriptionMap = '''{"en_US":"${roleDescriptionEncoded}"}''';
		}
		else {
			var roleDescriptionMap = "{}";
		}

		if (isSet(roleName)) {
			var roleNameEncoded = URLUtil.encode("${roleName}", "UTF-8");
		}
		else {
			fail("Please set 'roleName'.");
		}

		if (isSet(roleTitle)) {
			var roleTitleEncoded = URLUtil.encode("${roleTitle}", "UTF-8");

			var roleTitleMap = '''{"en_US":"${roleTitleEncoded}"}''';
		}
		else {
			var roleTitleMap = "{}";
		}

		if ("${roleType}" == "regular") {
			var roleTypeId = "1";
		}
		else if ("${roleType}" == "site") {
			var roleTypeId = "2";
		}
		else if ("${roleType}" == "organization") {
			var roleTypeId = "3";
		}
		else {
			fail("Invalid 'roleType', '${roleType}' must be 'regular/site/organization'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/role/add-role \
				-u test@liferay.com:test \
				-d className=com.liferay.portal.kernel.model.Role \
				-d classPK=0 \
				-d name=${roleNameEncoded} \
				-d titleMap=${roleTitleMap} \
				-d descriptionMap=${roleDescriptionMap} \
				-d type=${roleTypeId} \
				-d subtype=
		''';

		echo("## Adding a 'Role' with the following info using JSONWS:");

		if (isSet(roleDescription)) {
			echo("## * Role Description: ${roleDescription}");
		}

		echo("## * Role Name: ${roleName}");

		if (isSet(roleTitle)) {
			echo("## * Role Title: ${roleTitle}");
		}

		echo("## * Role Type: ${roleType}");

		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	macro _assignRoleToUser {
		if (!(isSet(roleId))) {
			fail("Please set 'roleId'.");
		}

		if (!(isSet(userId))) {
			fail("Please set 'userId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/add-role-users \
				-u test@liferay.com:test \
				-d roleId=${roleId} \
				-d userIds=${userId}
		''';

		var roleName = JSONRole.getRoleName(roleId = "${roleId}");

		var userEmailAddress = JSONUser.getUserEmailAddress(userId = "${userId}");

		echo("## Assigning a 'User' to a 'Role' with the following info using JSONWS:");

		echo("## * Role Name: ${roleName}");

		echo("## * User Email Address: ${userEmailAddress}");

		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	macro addOrganizationRole {
		if (!(isSet(roleName))) {
			fail("Please set 'roleName'.");
		}

		JSONRole._addRole(
			roleDescription = "${roleDescription}",
			roleName = "${roleName}",
			roleTitle = "${roleTitle}",
			roleType = "organization"
		);
	}

	macro addRegularRole {
		if (!(isSet(roleName))) {
			fail("Please set 'roleName'.");
		}

		JSONRole._addRole(
			roleDescription = "${roleDescription}",
			roleName = "${roleName}",
			roleTitle = "${roleTitle}",
			roleType = "regular"
		);
	}

	macro addSiteRole {
		if (!(isSet(roleName))) {
			fail("Please set 'roleName'.");
		}

		JSONRole._addRole(
			roleDescription = "${roleDescription}",
			roleName = "${roleName}",
			roleTitle = "${roleTitle}",
			roleType = "site"
		);
	}

	macro assignRoleToUser {
		if (!(isSet(roleName))) {
			fail("Please set 'roleName'.");
		}

		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		var roleId = JSONRole.getRoleIdByName(roleName = "${roleName}");

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		JSONRole._assignRoleToUser(roleId = "${roleId}", userId = "${userId}");
	}

	macro getRoleIdByName {
		if (isSet(roleName)) {
			var roleNameEncoded = URLUtil.encode("${roleName}", "UTF-8");
			var roleNameEncoded = StringUtil.replace("${roleNameEncoded}", "+", "%20");
		}
		else {
			fail("Please set 'roleName'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/role/get-role/company-id/${companyId}/name/${roleNameEncoded} \
				-u test@liferay.com:test
		''';
		var roleId = JSONCurlUtil.get("${curl}", "$['roleId']");

		return "${roleId}";
	}

	macro getRoleName {
		if (!(isSet(roleId))) {
			fail("Please set 'roleId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/role/get-role/role-id/${roleId} \
				-u test@liferay.com:test
		''';
		var roleName = JSONCurlUtil.get("${curl}", "$['name']");

		return "${roleName}";
	}
}