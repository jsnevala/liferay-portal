definition {
	macro configureBreadcrumb {
		IFrame.selectConfigurationFrame();

		if (isSet(checkSetupName)) {
			for (var checkSetupName : list "${checkSetupName}") {
				var key_checkSetupName = "${checkSetupName}";

				Check.checkToggleSwitch(locator1 = "ToggleSwitch#SHOW_SPECIFIC_TOGGLE_SWITCH");
			}
		}

		if (isSet(uncheckSetupName)) {
			for (var uncheckSetupName : list "${uncheckSetupName}") {
				var key_checkSetupName = "${uncheckSetupName}";

				Uncheck.uncheckToggleSwitch(
					locator1 = "ToggleSwitch#SHOW_SPECIFIC_TOGGLE_SWITCH"
				);
			}
		}

		if (isSet(selectADTType)) {
			Select(
				locator1 = "Configuration#SETUP_DISPLAY_SETTINGS_DISPLAY_TEMPLATE_SELECT",
				value1 = "${selectADTType}"
			);
		}

		if ("${configureWithoutSave}" == "true") {
			echo("Configuring Breadcrumb without saving to check bug.");
		}
		else {
			PortletEntry.save();
		}

		SelectFrame(value1 = "relative=top");
	}

	macro displayTemplatePreview {
		IFrame.selectConfigurationFrame();

		Select(locator1 = "Select#DISPLAY_TEMPLATE", value1 = "${displayStyle}");

		SelectFrame(value1 = "relative=top");
	}

	macro saveDisplayPreview {
		IFrame.selectConfigurationFrame();

		PortletEntry.save();

		SelectFrame(value1 = "relative=top");
	}

	macro viewActiveEntry {
		AssertTextEquals(
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY_ACTIVE_ENTRY",
			value1 = "${breadcrumbNameUppercase}",
			breadcrumbNameUppercase = StringUtil.upperCase("${breadcrumbName}")
		);
	}

	macro viewADTApplied {
		var key_breadcrumbADTType = "${breadcrumbADTType}";

		AssertElementPresent(locator1 = "BreadcrumbPortlet#BREADCRUMB_ADT");
	}

	macro viewConfigurationCheckboxes {
		IFrame.selectConfigurationFrame();

		if (isSet(checkSetupName)) {
			for (var checkSetupName : list "${checkSetupName}") {
				var key_checkSetupName = "${checkSetupName}";

				AssertChecked.assertCheckedNotVisible(
					locator1 = "ToggleSwitch#SHOW_SPECIFIC_TOGGLE_SWITCH"
				);
			}
		}

		if (isSet(uncheckSetupName)) {
			for (var uncheckSetupName : list "${uncheckSetupName}") {
				var key_checkSetupName = "${uncheckSetupName}";

				AssertNotChecked.assertNotCheckedNotVisible(
					locator1 = "ToggleSwitch#SHOW_SPECIFIC_TOGGLE_SWITCH"
				);
			}
		}

		SelectFrame(value1 = "relative=top");
	}

	macro viewConfigurationPreview {
		IFrame.selectConfigurationFrame();

		if (isSet(breadcrumbListVisible)) {
			for (var breadcrumbName : list "${breadcrumbListVisible}") {
				Breadcrumb.viewEntry(breadcrumbName = "${breadcrumbName}");
			}
		}

		if (isSet(breadcrumbListNotVisible)) {
			for (var breadcrumbName : list "${breadcrumbListNotVisible}") {
				var key_breadcrumbName = "${breadcrumbName}";

				AssertElementNotPresent(
					locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
					key_breadcrumbName = "${breadcrumbName}"
				);
			}
		}

		SelectFrame(value1 = "relative=top");
	}

	macro viewEntry {
		AssertTextEquals(
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
			value1 = "${breadcrumbNameUppercase}",
			breadcrumbNameUppercase = StringUtil.upperCase("${breadcrumbName}"),
			key_breadcrumbName = "${breadcrumbName}"
		);
	}

	macro viewPG {
		if (isSet(breadcrumbListVisible)) {
			for (var breadcrumbName : list "${breadcrumbListVisible}") {
				var key_breadcrumbName = "${breadcrumbName}";
				var breadcrumbNameUppercase = StringUtil.upperCase("${breadcrumbName}");

				AssertTextEquals(
					locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
					value1 = "${breadcrumbNameUppercase}"
				);
			}
		}

		if (isSet(breadcrumbListNotVisible)) {
			for (var breadcrumbName : list "${breadcrumbListNotVisible}") {
				AssertElementNotPresent(
					locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
					key_breadcrumbName = "${breadcrumbName}"
				);
			}
		}
	}

	macro viewSpecificPG {
		if (isSet(breadcrumbListNotVisible)) {
			for (var breadcrumbName : list "${breadcrumbListNotVisible}") {
				AssertElementNotPresent(
					locator1 = "BreadcrumbPortlet#BREADCRUMB_SPECIFIC_ENTRY",
					key_breadcrumbName = "${breadcrumbName}"
				);
			}
		}

		if (isSet(breadcrumbListVisible)) {
			for (var breadcrumbName : list "${breadcrumbListVisible}") {
				AssertElementPresent(
					locator1 = "BreadcrumbPortlet#BREADCRUMB_SPECIFIC_ENTRY",
					key_breadcrumbName = "${breadcrumbName}"
				);
			}
		}
	}
}