definition {
	macro addAssociatedAssetType {
		Panel.expandPanel(panelHeading = "Associated Asset Types");

		if (!(isSet(rowNumber))) {
			var rowNumber = "1";
		}

		Select(
			locator1 = "CategoriesEditVocabulary#ASSOCIATED_ASSET_TYPES_SELECT_N",
			value1 = "${assetType}",
			key_rowNumber = "${rowNumber}"
		);

		if ("${required}" == "true") {
			var key_rowNumber = "${rowNumber}";

			Check.checkToggleSwitch(locator1 = "ToggleSwitch#REQUIRED");
		}
	}

	@summary = "Add a vocabulary named '${vocabularyName}'"
	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${vocabularyName}");

		if (isSet(vocabularyDescription)) {
			PortletEntry.inputDescription(description = "${vocabularyDescription}");
		}

		if ("${allowMultipleCategories}" == "false") {
			Uncheck.uncheckToggleSwitch(
				locator1 = "CategoriesEditVocabulary#ALLOW_MULTIPLE_CATEGORIES_TOGGLE_SWITCH"
			);
		}

		if (isSet(localizationNameKey)) {
			var key_localizationName = "${localizationNameKey}";

			Click(locator1 = "Translation#LOCALIZATION_NAME");

			PortletEntry.inputName(name = "${localizationName}");
		}

		if (isSet(localization2NameKey)) {
			var key_localizationName = "${localization2NameKey}";

			Click(locator1 = "Translation#LOCALIZATION_NAME");

			PortletEntry.inputName(name = "${localization2Name}");
		}

		PortletEntry.save();
	}

	macro addViewableByCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${vocabularyName}");

		Panel.expandPanel(panelHeading = "Permissions");

		Select(locator1 = "Select#PERMISSIONS", value1 = "${viewableBy}");

		PortletEntry.save();

		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}"
		);
	}

	macro addVocabularyWithNullTitle {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = " ");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "Message#ERROR_FIELD_REQUIRED",
			value1 = "This field is required."
		);
	}

	macro addWithAssociatedAssetTypeCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${vocabularyName}");

		Vocabulary.addAssociatedAssetType(
			assetType = "${assetType}",
			required = "${required}",
			rowNumber = "${rowNumber}"
		);

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}",
			key_vocabularyName = "${vocabularyName}"
		);
	}

	macro deleteAssociatedAssetTypeViaVocabularyCP {
		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}",
			key_vocabularyName = "${vocabularyName}"
		);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${vocabularyName}"
		);

		Panel.expandPanel(panelHeading = "Associated Asset Types");

		Click(
			locator1 = "CategoriesEditVocabulary#ASSOCIATED_ASSET_TYPES_DELETE_BUTTON_2"
		);

		PortletEntry.save();
	}

	macro deleteByIconCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}"
		);

		Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = "${vocabularyName}");
	}

	macro deleteByMenuCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}"
		);

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${vocabularyName}"
		);

		Confirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();

		AssertTextNotPresent(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}"
		);
	}

	macro editCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}"
		);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${vocabularyName}"
		);

		if (isSet(vocabularyNameEdit)) {
			PortletEntry.inputTitle(title = "${vocabularyNameEdit}");
		}

		if ("${allowMultipleCategories}" == "false") {
			Uncheck.uncheckToggleSwitch(
				locator1 = "CategoriesEditVocabulary#ALLOW_MULTIPLE_CATEGORIES_TOGGLE_SWITCH"
			);
		}
		else if ("${allowMultipleCategories}" == "true") {
			Check.checkToggleSwitch(
				locator1 = "CategoriesEditVocabulary#ALLOW_MULTIPLE_CATEGORIES_TOGGLE_SWITCH"
			);
		}

		PortletEntry.save();

		if (isSet(vocabularyNameEdit)) {
			var key_vocabularyName = "${vocabularyNameEdit}";

			AssertTextEquals(
				locator1 = "Categories#VOCABULARY_ENTRY",
				value1 = "${vocabularyNameEdit}"
			);
		}
	}

	macro searchCP {
		Type(locator1 = "TextInput#SEARCH", value1 = "${vocabularySearchEntry}");

		Click(locator1 = "Icon#BASIC_SEARCH");
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Categories"
		);

		if (IsElementPresent(locator1 = "Checkbox#SELECT_ALL")) {
			Check.checkAll(locator1 = "Checkbox#SELECT_ALL");

			ClickNoError(locator1 = "Icon#DELETE");

			Confirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);
		}
	}

	macro viewCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}"
		);

		if ("${allowMultipleCategories}" == "false") {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = "${vocabularyName}"
			);

			AssertNotChecked.assertNotCheckedNotVisible(
				locator1 = "CategoriesEditVocabulary#ALLOW_MULTIPLE_CATEGORIES_TOGGLE_SWITCH"
			);
		}
	}

	macro viewSearchResultsCP {
		if (isSet(vocabularyNameList)) {
			for (var vocabularyName : list "${vocabularyNameList}") {
				var key_vocabularyName = "${vocabularyName}";

				AssertTextEquals(
					locator1 = "Categories#VOCABULARY_ENTRY",
					value1 = "${vocabularyName}"
				);
			}
		}

		if (isSet(vocabularyDescriptionList)) {
			for (var vocabularyDescription : list "${vocabularyDescriptionList}") {
				var key_vocabularyDescription = "${vocabularyDescription}";

				AssertTextEquals(
					locator1 = "Categories#VOCABULARY_ENTRY_DESCRIPTION",
					value1 = "${vocabularyDescription}"
				);
			}
		}

		if ("${nullResult}" == "true") {
			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "There are no vocabularies."
			);
		}
	}

	macro viewVocabularyPermissionsCP {
		var key_vocabularyName = "${vocabularyName}";

		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}"
		);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${vocabularyName}"
		);

		IFrame.selectDialogFrame();

		if ("${viewableBy}" == "Anyone (Guest Role)") {
			AssertChecked(locator1 = "AssetPermissions#GUEST_VIEW_CHECKBOX");
		}
		else if ("${viewableBy}" == "Owner") {
			AssertChecked(locator1 = "AssetPermissions#OWNER_VIEW_CHECKBOX");
		}
		else if ("${viewableBy}" == "Site Members") {
			AssertChecked(locator1 = "AssetPermissions#SITE_MEMBER_VIEW_CHECKBOX");
		}

		SelectFrameTop(value1 = "relative=top");
	}

	macro viewWithAssociatedAssetTypeCP {
		AssertTextEquals(
			locator1 = "Categories#VOCABULARY_ENTRY",
			value1 = "${vocabularyName}",
			key_vocabularyName = "${vocabularyName}"
		);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${vocabularyName}"
		);

		Panel.expandPanel(panelHeading = "Associated Asset Types");

		AssertSelectedLabel(
			locator1 = "CategoriesEditVocabulary#ASSOCIATED_ASSET_TYPES_SELECT",
			value1 = "${vocabularyAssociatedAssetType}"
		);
	}
}