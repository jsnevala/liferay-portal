definition {
	macro _addTag {
		Type(locator1 = "AssetCategorization#TAGS_FIELD", value1 = "${tagName}");

		if (IsElementPresent(locator1 = "AssetCategorization#TAG_AUTOCOMPLETE_CONTAINER")) {
			Click(locator1 = "AssetCategorization#TAG_AUTOCOMPLETE");
		}

		AssertClick(locator1 = "Button#ADD_TAGS", value1 = "Add");
	}

	macro addCategory {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(locator1 = "AssetCategorization#CATEGORY_SELECT", value1 = "Select");

		AssetCategorization.gotoSelectCategory();

		Portlet.selectTreeNode(nodeName = "${categoryName}");

		SelectFrameTop();

		if (IsElementPresent(locator1 = "Button#DONE")) {
			AssertClick(locator1 = "Button#DONE", value1 = "Done");
		}
		else {
			Click(locator1 = "Icon#CLOSE");
		}

		if (IsElementPresent(locator1 = "IFrame#MODAL_IFRAME")) {
			IFrame.selectModalFrame();
		}

		AssertTextEquals(
			locator1 = "AssetCategorization#CATEGORY",
			value1 = "${categoryName}",
			key_categoryName = "${categoryName}"
		);
	}

	macro addMultipleCategories {
		var key_vocabularyName = "${vocabularyName}";

		AssertClick(locator1 = "AssetCategorization#CATEGORY_SELECT", value1 = "Select");

		AssetCategorization.gotoSelectCategory();

		for (var categoryName : list "${categoryNameList}") {
			Portlet.selectTreeNode(nodeName = "${categoryName}");
		}

		Click(locator1 = "Icon#CLOSE");
	}

	macro addTag {
		AssetCategorization._addTag(tagName = "${tagName}");

		AssertTextEquals(
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}",
			key_tagName = "${tagName}"
		);
	}

	macro gotoSelectCategory {
		SelectFrameTop();

		if (IsElementPresent(locator1 = "IFrame#SELECT_CATEGORY")) {
			IFrame.selectCategoryFrame();
		}

		Portlet.expandTree();
	}

	macro removeTag {
		var key_tagName = "${tagName}";

		AssertTextEquals(locator1 = "AssetCategorization#TAG", value1 = "${tagName}");

		Click(locator1 = "AssetCategorization#TAG_REMOVE");

		AssertElementNotPresent(locator1 = "AssetCategorization#TAG");
	}

	macro selectTag {
		Click(locator1 = "AssetCategorization#TAGS_SELECT_BUTTON");

		SelectFrameTop();

		if (IsElementPresent(locator1 = "IFrame#SELECT_TAG")) {
			SelectFrame(locator1 = "IFrame#SELECT_TAG");
		}

		for (var tagName : list "${tagNameList}") {
			Click(locator1 = "AssetCategorization#TAG_CHECKBOX", key_tagName = "${tagName}");
		}

		SelectFrameTop();

		if (IsElementPresent(locator1 = "Button#DONE")) {
			AssertClick(locator1 = "Button#DONE", value1 = "Done");
		}
		else {
			Click(locator1 = "Icon#CLOSE");
		}

		if (IsElementPresent(locator1 = "IFrame#MODAL_IFRAME")) {
			IFrame.selectModalFrame();
		}

		AssertTextEquals(
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}",
			key_tagName = "${tagName}"
		);
	}
}