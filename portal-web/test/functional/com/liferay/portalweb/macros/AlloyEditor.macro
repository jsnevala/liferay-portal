definition {
	macro addContent {
		Type.typeEditor(locator1 = "AlloyEditor#CONTENT_FIELD", value1 = "${content}");
	}

	macro addLocalizedContent {
		Type.typeEditor(
			locator1 = "AlloyEditor#CONTENT_FIELD_LOCALIZED_1",
			value1 = "${content}"
		);
	}

	macro addSourceContent {
		AlloyEditor.clickContentField();

		Click(locator1 = "Button#EDITOR_SWITCH");

		Type.sendKeysAceEditor(locator1 = "TextArea#ACE_EDITOR", value1 = "${content}");
	}

	macro addTitle {
		Type.typeEditor(locator1 = "AlloyEditor#TITLE_FIELD", value1 = "${title}");
	}

	macro addTitleAndContent {
		AlloyEditor.addTitle(title = "${title}");

		AlloyEditor.addContent(content = "${content}");
	}

	macro clickContentField {
		Click(locator1 = "AlloyEditor#CONTENT_FIELD");
	}

	macro toggleStylingInAlloyEditor {
		SelectFieldText(locator1 = "AlloyEditor#CONTENT_FIELD");

		if (isSet(boldContent)) {
			Click(locator1 = "AlloyEditor#TEXT_FORMATTING_BOLD");
		}

		if (isSet(italicContent)) {
			Click(locator1 = "AlloyEditor#TEXT_FORMATTING_ITALIC");
		}

		if (isSet(underlineContent)) {
			Click(locator1 = "AlloyEditor#TEXT_FORMATTING_UNDERLINE");
		}
	}

	macro viewNoStylingInAlloyEditor {
		AlloyEditor.clickContentField();

		if (isSet(boldContent)) {
			AssertElementNotPresent(
				locator1 = "AlloyEditor#CONTENT_FIELD_BOLD",
				key_boldContent = "${boldContent}"
			);
		}

		if (isSet(italicContent)) {
			AssertElementNotPresent(
				locator1 = "AlloyEditor#CONTENT_FIELD_ITALIC",
				key_italicContent = "${italicContent}"
			);
		}

		if (isSet(underlineContent)) {
			AssertElementNotPresent(
				locator1 = "AlloyEditor#CONTENT_FIELD_UNDERLINE",
				key_underlineContent = "${underlineContent}"
			);
		}
	}

	macro viewStylingInAlloyEditor {
		AlloyEditor.clickContentField();

		if (isSet(boldContent)) {
			AssertElementPresent(
				locator1 = "AlloyEditor#CONTENT_FIELD_BOLD",
				key_boldContent = "${boldContent}"
			);
		}

		if (isSet(italicContent)) {
			AssertElementPresent(
				locator1 = "AlloyEditor#CONTENT_FIELD_ITALIC",
				key_italicContent = "${italicContent}"
			);
		}

		if (isSet(underlineContent)) {
			AssertElementPresent(
				locator1 = "AlloyEditor#CONTENT_FIELD_UNDERLINE",
				key_underlineContent = "${underlineContent}"
			);
		}
	}
}