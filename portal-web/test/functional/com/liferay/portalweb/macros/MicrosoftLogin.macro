definition {
	macro accountSignIn {
		var key_userEmail = PropsUtil.get("sharepoint.oauth.user.name");
		var userPassword = PropsUtil.get("sharepoint.oauth.user.password");

		if (IsElementPresent.pauseIsElementPresent(locator1 = "MicrosoftLogin#LOGIN_USER_CHOOSER")) {
			AssertClick(
				locator1 = "MicrosoftLogin#LOGIN_USER_CHOOSER",
				value1 = "${key_userEmail}"
			);

			Type(locator1 = "TextInput#LOGIN_PASSWORD_FIELD", value1 = "${userPassword}");

			AssertClick(
				locator1 = "MicrosoftLogin#LOGIN_SIGN_IN_BUTTON",
				value1 = "Sign in"
			);
		}
		else if (IsElementPresent.pauseIsElementPresent(locator1 = "TextInput#LOGIN_EMAIL_FIELD")) {
			Type(locator1 = "TextInput#LOGIN_EMAIL_FIELD", value1 = "${key_userEmail}");

			AssertClick(locator1 = "MicrosoftLogin#LOGIN_NEXT_BUTTON", value1 = "Next");

			Type(locator1 = "TextInput#LOGIN_PASSWORD_FIELD", value1 = "${userPassword}");

			AssertClick(
				locator1 = "MicrosoftLogin#LOGIN_SIGN_IN_BUTTON",
				value1 = "Sign in"
			);
		}

		AssertClick(locator1 = "MicrosoftLogin#SHAREPOINT_YES_BUTTON", value1 = "Yes");
	}

	macro confirmSharepointApp {
		Pause(locator1 = "3000");

		var app_name = "Collaboration Test Application";

		if (IsElementPresent.pauseIsElementPresent(locator1 = "MicrosoftLogin#SHAREPOINT_NOTIFICATION")) {
			AssertClick(
				locator1 = "MicrosoftLogin#SHAREPOINT_TRUST_IT_BUTTON",
				value1 = "Trust It"
			);
		}

		if (IsElementPresent(locator1 = "Portlet#HEADER")) {
			AssertTextEquals(locator1 = "Portlet#HEADER", value1 = "${dmRepositoryName}");
		}
		else {
			AssertTextEquals(
				locator1 = "MicrosoftLogin#SHAREPOINT_SITE_TITLE",
				value1 = "Sitio de grupo de Liferay"
			);
		}
	}
}