definition {
	@summary = "Navigate to the Live site"
	macro gotoLiveSite {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		AssertClick(locator1 = "ProductMenu#LIVE_LINK", value1 = "Live");
	}

	@summary = "Navigate to Navigate to Product Menu > Sites > ${category} > ${page}"
	macro gotoLocalizedEditPage {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.expandCategory(category = "${category}");

		var key_page = "${page}";

		Click.mouseOverClick(locator1 = "SitePages#SPECIFIC_PAGE_ELLIPSIS");

		Click(locator1 = "SitePages#EDIT_SPECIFIC_PAGE");
	}

	@summary = "Navigate to Product Menu > Sites > Sites Modal > ${site}"
	macro gotoLocalizedSite {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.openSitesPopup();

		IFrame.selectDialogFrame();

		Navigator.gotoNavItem(navItem = "${navItem}");

		SiteSelector.selectSite(site = "${site}");

		ProductMenuHelper.expandPanel(panel = "Site Administration");

		AssertTextEquals.assertPartialText(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
			value1 = "${site}"
		);
	}

	@summary = "Navigate to Product Menu > Profile"
	macro gotoPersonalSite {
		ProductMenuHelper.expandPanel(panel = "User");

		Click(locator1 = "ProductMenu#USER_PROFILE");
	}

	@summary = "Navigate to Product Menu > ${panel} > ${category} > ${portlet}"
	macro gotoPortlet {
		ProductMenuHelper.expandPanel(panel = "${panel}");

		ProductMenuHelper.expandCategory(category = "${category}");

		ProductMenuHelper.gotoPortlet(portlet = "${portlet}");
	}

	@summary = "Navigate to Product Menu > Sites > Navigation > Private Pages"
	macro gotoPrivatePages {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.expandCategory(category = "Navigation");

		if (IsElementNotPresent(locator1 = "ProductMenu#PRIVATE_PAGES_SELECTED")) {
			AssertClick(locator1 = "ProductMenu#PRIVATE_PAGES", value1 = "Private Pages");
		}

		Pause(locator1 = "5000");

		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandCategory(category = "Navigation");
	}

	@summary = "Navigate to Product Menu > Sites > Navigation > Public Pages > ${page}"
	macro gotoPublicPages {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.expandCategory(category = "Navigation");

		if (IsElementNotPresent(locator1 = "ProductMenu#PUBLIC_PAGES_SELECTED")) {
			Click(locator1 = "ProductMenu#PUBLIC_PAGES");
		}

		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandCategory(category = "Navigation");
	}

	@summary = "Navigate to Product Menu > Sites > Sites Modal > ${site}"
	macro gotoSite {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.openSitesPopup();

		IFrame.selectDialogFrame();

		Navigator.gotoNavItem(navItem = "My Sites");

		SiteSelector.selectSite(site = "${site}");

		ProductMenuHelper.expandPanel(panel = "Site Administration");

		AssertTextEquals.assertPartialText(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
			value1 = "${site}"
		);
	}

	@summary = "Navigate to ${site} via the 'Go to Site' link"
	macro gotoSiteViaLink {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		AssertClick(locator1 = "ProductMenu#GO_TO_SITES_LINK", value1 = "Go to Site");
	}

	@summary = "Navigate to the Staging site"
	macro gotoStagingSite {
		if (IsElementNotPresent(locator1 = "ProductMenu#STAGING_SELECTED")) {
			ProductMenuHelper.expandPanel(panel = "Site Administration");

			AssertClick(locator1 = "ProductMenu#STAGING_LINK", value1 = "Staging");
		}

		AssertVisible(locator1 = "ProductMenu#STAGING_SELECTED");
	}

	@summary = "View ${site} is not available in the product menu."
	macro viewNoSite {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		if ((IsElementPresent(locator1 = "ProductMenu#BACK_ARROW")) && (IsVisible(locator1 = "ProductMenu#BACK_ARROW"))) {
			Click(locator1 = "ProductMenu#BACK_ARROW");
		}

		ProductMenuHelper.viewNoSite(site = "${site}");
	}
}