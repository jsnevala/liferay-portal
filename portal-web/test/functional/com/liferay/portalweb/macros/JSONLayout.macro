definition {

	macro _addLayout {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		if (isSet(layoutName)) {
			var layoutNameEncoded = URLUtil.encode("${layoutName}", "UTF-8");
		}
		else {
			fail("Please set 'layoutName'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/layout/add-layout \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d privateLayout=false \
				-d parentLayoutId=0 \
				-d name=${layoutNameEncoded} \
				-d title= \
				-d description= \
				-d type=portlet \
				-d hidden=false \
				-d friendlyURL=
		''';

		var groupName = JSONGroup.getGroupName(groupId = "${groupId}");

		echo("## Adding a 'Layout' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Layout Name: ${layoutName}");

		var layoutId = JSONCurlUtil.post("${curl}", "$['layoutId']");
	}

	macro addLayoutByGroupName {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(layoutName))) {
			fail("Please set 'layoutName'.");
		}

		var groupId = JSONGroup.getGroupIdByName(groupName = "${groupName}");

		JSONLayout._addLayout(groupId = "${groupId}", layoutName = "${layoutName}");
	}
}