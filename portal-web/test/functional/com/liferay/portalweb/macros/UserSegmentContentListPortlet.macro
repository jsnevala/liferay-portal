definition {
	macro configureDisplayFilter {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration"
		);

		IFrame.selectConfigurationFrame();

		Panel.expandPanel(panelHeading = "Source");

		if (isSet(assetTypeFilter)) {
			Select(
				locator1 = "UserSegmentContentList#SELECT_ASSET_TYPE",
				value1 = "${assetTypeFilter}"
			);
		}

		if (isSet(selectMultipleAssetTypes)) {
			Select(
				locator1 = "UserSegmentContentList#SELECT_ASSET_TYPE",
				value1 = "Select More Than One..."
			);

			while (IsElementPresent(locator1 = "UserSegmentContentList#SELECTED_ASSETS")) {
				Click(locator1 = "UserSegmentContentList#SELECTED_ASSETS");

				Click(locator1 = "UserSegmentContentList#MOVE_SELECTED_TO_AVAILABLE_BUTTON");
			}

			for (var assetType : list "${selectMultipleAssetTypes}") {
				var key_assetType = "${assetType}";

				Click(locator1 = "UserSegmentContentList#AVAILABLE_ASSET_TYPE");

				Click(locator1 = "UserSegmentContentList#MOVE_AVAILABLE_TO_SELECTED_BUTTON");

				AssertTextEquals(
					locator1 = "UserSegmentContentList#SELECTED_ASSETS",
					value1 = "${assetType}"
				);
			}

			PortletEntry.save();

			Select(
				locator1 = "UserSegmentContentList#SELECT_ASSET_TYPE",
				value1 = "Select More Than One..."
			);

			for (var assetType : list "${selectMultipleAssetTypes}") {
				var key_assetType = "${assetType}";

				AssertTextEquals(
					locator1 = "UserSegmentContentList#SELECTED_ASSETS",
					value1 = "${assetType}"
				);
			}
		}

		PortletEntry.save();
	}

	macro viewContent {
		for (var entryContent : list "${viewEntryContentList}") {
			var key_entryContent = "${entryContent}";

			AssertTextEquals(
				locator1 = "UserSegmentContentList#VIEW_ENTRY_CONTENT",
				value1 = "${entryContent}"
			);
		}
	}

	macro viewContentNotPresent {
		AssertElementNotPresent(locator1 = "AP#ASSET_ABSTRACT_DETAIL_CONTENT");
	}
}