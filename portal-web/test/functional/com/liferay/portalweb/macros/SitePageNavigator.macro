definition {
	macro gotoAddPage {
		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.expandCategory(category = "Navigation");

		ProductMenuHelper.expandPageTree();

		if ((IsElementNotPresent(locator1 = "ProductMenu#PUBLIC_PAGES")) && (IsElementNotPresent(locator1 = "ProductMenu#PRIVATE_PAGES"))) {
			ProductMenuHelper.addFirstPage(sitePagePrivacy = "${sitePagePrivacy}");
		}
		else {
			if ("${sitePagePrivacy}" == "Private") {
				if (IsElementNotPresent(locator1 = "ProductMenu#PRIVATE_PAGES")) {
					Click(locator1 = "SitePages#PUBLIC_PAGE_ELLIPSIS");

					AssertClick.assertPartialTextClickAt(
						locator1 = "MenuItem#ANY_MENU_ITEM",
						value1 = "Add Private Page",
						key_menuItem = "Add Private Page"
					);
				}
				else if (IsElementNotPresent(locator1 = "ProductMenu#PUBLIC_PAGES")) {
					Click(locator1 = "SitePages#PRIVATE_PAGE_ELLIPSIS");

					AssertClick.assertPartialTextClickAt(
						locator1 = "MenuItem#ANY_MENU_ITEM",
						value1 = "Add Private Page",
						key_menuItem = "Add Private Page"
					);
				}
				else {
					ProductMenu.gotoPrivatePages();

					Click(locator1 = "SitePages#PRIVATE_PAGE_ELLIPSIS");

					AssertClick.assertPartialTextClickAt(
						locator1 = "MenuItem#ANY_MENU_ITEM",
						value1 = "Add Page",
						key_menuItem = "Add Page"
					);
				}
			}
			else {
				ProductMenu.gotoPublicPages();

				Click(locator1 = "SitePages#PUBLIC_PAGE_ELLIPSIS");

				if (IsElementPresent(locator1 = "ProductMenu#PRIVATE_PAGES")) {
					AssertClick.assertPartialTextClickAt(
						locator1 = "MenuItem#ANY_MENU_ITEM",
						value1 = "Add Page",
						key_menuItem = "Add Page"
					);
				}
				else {
					AssertClick.assertPartialTextClickAt(
						locator1 = "MenuItem#ANY_MENU_ITEM",
						value1 = "Add Public Page",
						key_menuItem = "Add Public Page"
					);
				}
			}
		}
	}

	@summary = "Navigate to Product Menu > Sites > Navigation > Public Pages > ${page}"
	macro gotoEditPage {
		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.expandCategory(category = "Navigation");

		ProductMenuHelper.expandPageTree();

		if ("${sitePagePrivacy}" == "Private Pages") {
			Click(locator1 = "ProductMenu#PRIVATE_PAGES");
		}

		var key_page = "${page}";

		if ("${page}" == "Public Pages") {
			Click(locator1 = "SitePages#PUBLIC_PAGE_ELLIPSIS");

			AssertClick.assertPartialTextClickAt(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Configure",
				key_menuItem = "Configure"
			);
		}
		else if ("${page}" == "Private Pages") {
			Click(locator1 = "SitePages#PRIVATE_PAGE_ELLIPSIS");

			AssertClick.assertPartialTextClickAt(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Configure",
				key_menuItem = "Configure"
			);
		}
		else {
			Click.mouseOverClick(locator1 = "SitePages#SPECIFIC_PAGE_ELLIPSIS");

			AssertClick.assertPartialTextClickAt(
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Configure Page",
				key_menuItem = "Configure Page"
			);
		}
	}

	macro gotoPages {
		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandPanel(panel = "Site Administration");

		ProductMenuHelper.expandCategory(category = "Navigation");

		ProductMenuHelper.expandPageTree();

		if ("${page}" == "Private Pages") {
			Click(locator1 = "ProductMenu#PRIVATE_PAGES");
		}
	}
}