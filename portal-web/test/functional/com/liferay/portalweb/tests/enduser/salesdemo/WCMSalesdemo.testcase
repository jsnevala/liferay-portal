@component-name = "portal-wcm"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Display";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.tearDownCP();

			Site.tearDownCP();

			Page.tearDownPG();
		}
	}

	@priority = "4"
	test IFrameDemo {
		Page.add(
			columnName = "1 Column",
			pageName = "IFrame Page",
			sitePageType = "Empty Page"
		);

		Navigator.gotoPage(pageName = "IFrame Page");

		Portlet.addPG(portletName = "IFrame");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		IFrame.addCP(sourceURL = "http://iscaliforniaonfire.com");

		Navigator.gotoPage(pageName = "IFrame Page");

		SelectFrame(locator1 = "IFrame#IFRAME_PORTLET");

		AssertTextEquals.assertPartialText(
			locator1 = "Embedded#EMBEDDED_TEXT_UPDATED",
			value1 = "updated"
		);
	}

	@priority = "4"
	test SalesDemo {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "DHS Content",
			webContentTitle = "DHS Title"
		);

		PortletEntry.publish();

		Navigator.openURL();

		SitePageNavigator.gotoEditPage(page = "Welcome");

		Page.editPageLayoutColumn(columnName = "2 Columns (50/50)");

		Navigator.gotoPage(pageName = "Welcome");

		Content.addPG(assetTitle = "DHS Title", webContentContent = "DHS Content");

		WebContent.viewPGViaWCD(
			webContentContent = "DHS Content",
			webContentTitle = "DHS Title"
		);

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations"
		);

		User.addCP(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn"
		);

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations"
		);

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn"
		);

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln"
		);

		WebContent.viewNoEditWCViaWCD();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "Test Test",
			webContentContent = "DHS Content"
		);

		Page.add(pageName = "Sales Page");

		Navigator.gotoPage(pageName = "Sales Page");

		MobileDevice.previewPG(deviceType = "tablet");

		Navigator.openURL();

		MobileDevice.previewPG(deviceType = "smartphone");
	}
}