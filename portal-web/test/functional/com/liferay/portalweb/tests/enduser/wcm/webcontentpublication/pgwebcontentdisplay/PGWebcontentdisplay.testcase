@component-name = "portal-wcd"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Display";
	var pageName = "Web Content Display Page";
	var portletName = "Web Content Display";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Page.add(pageName = "Web Content Display Page");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		Portlet.addPG(portletName = "Web Content Display");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();

			WebContent.tearDownCP();

			Page.tearDownPG();
		}
	}

	@priority = "4"
	test AddDraftNotApproved {
		WebContent.addDraftPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.viewPGViaWCD(draft = "true", webContentTitle = "WC WebContent Title");
	}

	@priority = "4"
	test AddNewWebContentViaAddContentPanel {
		Page.add(pageName = "Web Content Page");

		Navigator.gotoPage(pageName = "Web Content Page");

		Content.addNewPG(assetType = "Basic Web Content");

		IFrame.selectEditAssetFrame();

		WebContent.addCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.publishViaWCD();

		Navigator.gotoPage(pageName = "Web Content Page");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@priority = "4"
	test AddPortletWCDSite {
		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Page.add(pageName = "Site Page", siteName = "Site Name");

		Navigator.gotoSitePage(pageName = "Site Page", siteName = "Site Name");

		Portlet.addPG(portletName = "Web Content Display");
	}

	@priority = "5"
	test AddWebContent {
		property portal.acceptance = "true";

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@priority = "3"
	test AddWebContentPortalScopeCurrentPage {
		for (var i : list "1,2,3") {
			Page.add(pageName = "Web Content Display Page${i}");

			Portlet.addPG(portletName = "Web Content Display");
		}

		Navigator.gotoPage(pageName = "Web Content Display Page1");

		WebContentDisplayPortlet.configureScopePG(
			scopeSelection = "Web Content Display Page1"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page1");

		WebContentDisplayPortlet.viewConfigureScopePG(
			scopeSelection = "Web Content Display Page1"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page2");

		WebContentDisplayPortlet.configureScopePG(
			scopeSelection = "Web Content Display Page1"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page2");

		WebContentDisplayPortlet.viewConfigureScopePG(
			scopeSelection = "Web Content Display Page1"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page3");

		WebContentDisplayPortlet.configureScopePG(
			scopeSelection = "Web Content Display Page3"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page1");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		Navigator.gotoPage(pageName = "Web Content Display Page2");

		WebContent.viewDefaultPGViaWCD();

		WebContentDisplayPortlet.selectWebContent(
			webContentTitle = "WC WebContent Title"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page2");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		Navigator.gotoPage(pageName = "Web Content Display Page3");

		WebContent.viewDefaultPGViaWCD();

		WebContent.viewSelectNoPGViaWCD(webContentTitle = "WC WebContent Title");
	}

	@priority = "5"
	test AddWebContentViaAddContentPanel {
		property testray.component.names = "Training";

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		PortletEntry.publish();

		WebContent.viewTitle(webContentTitle = "WC WebContent Title");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		Content.addPG(
			assetTitle = "WC WebContent Title",
			pageName = "Web Content Display Page",
			webContentContent = "WC WebContent Content"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@priority = "5"
	test AddWithStructure {
		property testray.component.names = "Training";

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "Text",
			fieldName = "Text"
		);

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageTemplates();

		WebContentTemplates.addCP(
			templateFieldNames = "Text",
			templateLanguage = "ftl",
			templateName = "WC Template Name"
		);

		WebContentTemplates.addStructure(
			structureName = "WC Structure Name",
			templateFieldNames = "Text",
			templateLanguage = "ftl"
		);

		DDLDataDefinition.save();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addStructuredPGViaWCD(
			structureFieldName = "Text",
			structureName = "WC Structure Name",
			webContentText = "WC WebContent Text",
			webContentTitle = "WC WebContent Title"
		);
	}

	@priority = "4"
	test AssertExpired {
		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContent.expireSpecificVersionCP(webContentTitle = "WC WebContent Title");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.viewPGViaWCD(
			expiredContent = "true",
			webContentTitle = "WC WebContent Title"
		);
	}

	@priority = "4"
	test ChangeTemplate {
		Navigator.openURL();

		Page.add(pageName = "Web Content Page");

		Navigator.gotoPage(pageName = "Web Content Page");

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageTemplates();

		WebContentTemplates.addCP(
			templateFieldNames = "Content,Content,Content",
			templateLanguage = "ftl",
			templateName = "Second Basic Template"
		);

		WebContentTemplates.addStructure(
			structureName = "Basic Web Content",
			templateFieldNames = "Content,Content,Content",
			templateLanguage = "ftl"
		);

		DDLDataDefinition.save();

		Navigator.gotoPage(pageName = "Web Content Page");

		Content.addNewPG(assetType = "Basic Web Content");

		IFrame.selectEditAssetFrame();

		WebContent.addCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.selectTemplateCP(
			structureName = "Basic Web Content",
			templateName = "Second Basic Template"
		);

		WebContent.publishViaWCD();

		Navigator.gotoPage(pageName = "Web Content Page");

		WebContent.viewPGViaWCD(
			webContentContent = "WC WebContent Content WC WebContent Content WC WebContent Content"
		);

		WebContentNavigator.gotoEditPGViaWCD();

		WebContent.selectTemplateCP(
			structureName = "Basic Web Content",
			templateName = "Basic Web Content"
		);

		WebContent.publishViaWCD();

		Navigator.gotoPage(pageName = "Web Content Page");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@priority = "4"
	test EditWebContent {
		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		WebContentNavigator.gotoEditPGViaWCD();

		WebContent.editPGViaWCD(
			webContentContentEdit = "WC WebContent Content Edit",
			webContentTitleEdit = "WC WebContent Title Edit"
		);
	}

	@priority = "4"
	test LocalizeWebContent {
		property test.name.skip.portal.instance = "PGWebcontentdisplay#LocalizeWebContent";

		Navigator.gotoPage(pageName = "Web Content Display Page");

		Portlet.addPG(portletName = "Language Selector");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		WebContentNavigator.gotoEditPGViaWCD();

		WebContent.addTranslation(
			localizedContent = "世界您好 Content",
			localizedLanguage = "Chinese (China)",
			localizedTitle = "世界您好 Title",
			webContentTitle = "WC WebContent Title"
		);

		Button.clickPublish();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		LanguagePortlet.selectLanguagePG(language = "Chinese (China)");

		WebContent.viewPGViaWCD(webContentContent = "世界您好 Content");

		LanguagePortlet.tearDownLanguagePG();

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@priority = "5"
	test RemoveWCDPortletSite {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		Portlet.deletePG(portletName = "Web Content Display");
	}

	@priority = "4"
	test SearchWebContent {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		PortletEntry.publish();

		WebContent.viewTitle(webContentTitle = "WC WebContent Title");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.searchWebContent(
			webContentTitle = "WC WebContent Title"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.searchWebContentNo(
			webContentTitle = "WC2 WebContent2 Title2"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.searchWebContent(
			webContentSearchValue = "&quot;WC WebContent Title&quot;",
			webContentTitle = "WC WebContent Title"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.searchWebContentNo(
			webContentSearchValue = "&quot;WC2 WebContent2 Title2&quot;",
			webContentTitle = "WC2 WebContent2 Title2"
		);
	}

	@priority = "5"
	test SelectWebContent {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		PortletEntry.publish();

		WebContent.viewTitle(webContentTitle = "WC WebContent Title");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.selectWebContent(
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@priority = "3"
	test SelectWebContentScopeGlobal {
		ProductMenu.gotoSite(site = "Global");

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		PortletEntry.publish();

		WebContent.viewTitle(webContentTitle = "WC WebContent Title");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.configureScopePG(scopeSelection = "Global");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.viewConfigureScopePG(scopeSelection = "Global");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.selectWebContent(
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@priority = "4"
	test ViewAllFileTypesEnabled {
		property test.name.skip.portal.instance = "PGWebcontentdisplay#ViewAllFileTypesEnabled";

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Server Administration"
		);

		ServerAdministration.configureExternalServices(
			enableOpenOfficeIntegration = "true"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page");

		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Configuration"
		);

		WebContentDisplayPortlet.configureSetupPG(userToolNameList = "DOC,ODT,PDF,TXT");

		WebContentDisplayPortlet.saveConfiguration();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.viewEnabledFileTypesPGViaWCD(
			fileTypes = "DOC,ODT,PDF,TXT"
		);
	}

	@priority = "4"
	test ViewAllSettingsEnabledWCDConfiguration {
		property test.name.skip.portal.instance = "PGWebcontentdisplay#ViewAllSettingsEnabledWCDConfiguration";

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "Web Content Content 1",
			webContentTitle = "Web Content Title 1"
		);

		PortletEntry.publish();

		WebContent.viewTitle(webContentTitle = "Web Content Title 1");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "Web Content Content 2",
			webContentTitle = "Web Content Title 2"
		);

		PortletEntry.publish();

		WebContent.viewTitle(webContentTitle = "Web Content Title 2");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title 1");

		WebContent.addExistingRelatedAssetCP(
			assetTitle = "Web Content Title 2",
			assetType = "Basic Web Content",
			siteName = "Liferay",
			webContentTitle = "Web Content Title 1"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.selectWebContent(
			webContentTitle = "Web Content Title 1"
		);

		Navigator.gotoPage(pageName = "Web Content Display Page");

		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Configuration"
		);

		WebContentDisplayPortlet.configureSetupPG(
			enableViewCountIncrement = "true",
			metadataNameList = "Related Assets,Comment Ratings,Comments,Ratings",
			userToolNameList = "Print"
		);

		WebContentDisplayPortlet.saveConfiguration();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.viewAllSettingsEnabledPG(
			relatedAssetTitle = "Web Content Title 2",
			webContentContent = "Web Content Content 1"
		);
	}

	@priority = "4"
	test ViewArticleWithGuestViewPermission {
		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		User.loginPG(password = "test", userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		PermissionsInline.gotoConfigurationPG(portletName = "Web Content Display");

		Role.viewPermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			permissionFlagged = "true",
			roleName = "guest"
		);

		Role.removePermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			roleName = "guest"
		);

		User.logoutPG();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		Portlet.viewNoPermissionPG();

		User.loginPG(password = "test", userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		PermissionsInline.gotoConfigurationPG(portletName = "Web Content Display");

		Role.definePermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			roleName = "guest"
		);

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "WC WebContent Title"
		);

		IFrame.selectPermissionsFrame();

		Role.viewPermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			permissionFlagged = "true",
			roleName = "guest"
		);

		Role.removePermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			roleName = "guest"
		);

		User.logoutPG();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		User.loginPG(password = "test", userEmailAddress = "test@liferay.com");
	}

	@priority = "4"
	test ViewEditTemplate {
		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		WebContentDisplayPortlet.editTemplate(
			editTemplateButton = "true",
			structureName = "Basic Web Content",
			templateName = "Template Edit",
			templateScript = "Template Edited Extra Content",
			webContentContent = "WC WebContent Content"
		);
	}

	@priority = "4"
	test ViewPortletWCDShowAvailableLocales {
		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		WebContentNavigator.gotoEditPGViaWCD();

		WebContent.addTranslation(
			localizedContent = "世界您好 Content",
			localizedLanguage = "Chinese (China)",
			localizedTitle = "世界您好 Title",
			webContentTitle = "WC WebContent Title"
		);

		Button.clickPublish();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Configuration"
		);

		WebContentDisplayPortlet.configureSetupPG(userToolNameList = "Translations");

		WebContentDisplayPortlet.saveConfiguration();

		Navigator.gotoPage(pageName = "Web Content Display Page");

		Click(locator1 = "Language#LANGUAGE_MENU_CHINESE");

		WebContent.viewPGViaWCD(webContentContent = "世界您好 Content");

		Click(locator1 = "Language#LANGUAGE_MENU_ENGLISH");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@priority = "4"
	test ViewWCDWithDeletedWCArticle {
		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContent.deleteCP(webContentTitle = "WC WebContent Title");

		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContentDisplayPortlet.viewDeletedArticlePGViaWCD(
			webContentTitle = "WC WebContent Title"
		);
	}

	@priority = "4"
	test ViewWithPaginationScript {
		Navigator.gotoPage(pageName = "Web Content Display Page");

		WebContent.addPGViaWCD(
			webContentContent = "@page_break@ Welcome @page_break@",
			webContentTitle = "WC WebContent Title"
		);

		WebContent.viewPageBreak();

		WebContentDisplayPortlet.viewContentPageBreak(webContentContent = "Welcome");
	}
}