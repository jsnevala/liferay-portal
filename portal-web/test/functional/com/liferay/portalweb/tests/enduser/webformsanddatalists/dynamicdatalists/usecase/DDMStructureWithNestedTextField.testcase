@component-name = "portal-web-forms-and-data-lists"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Dynamic Data Lists";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);

		DDLDataDefinition.addCP();

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "Text Parent",
			fieldName = "textParent"
		);

		DynamicDataMapping.addFieldNested(
			field = "Text",
			fieldFieldLabel = "Text Child",
			fieldName = "textChild"
		);

		DDLDataDefinition.save();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);

		DDLList.addCP();

		DDLDataDefinition.selectCP();

		DDLDataDefinition.save();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.firstLoginPG();

			DDLList.tearDownCP();

			DDLDataDefinition.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-69335."
	@priority = "4"
	test ViewFieldTranslations {
		DDLRecord.addCP();

		DDLRecord.populateFieldText(
			ddlRecordFieldData = "English (United States) Parent",
			fieldName = "textParent"
		);

		DDLRecord.populateFieldText(
			ddlRecordFieldData = "English (United States) Child",
			fieldName = "textChild"
		);

		for (var language : list "Spanish (Spain),Portuguese (Brazil)") {
			DDLRecord.addTranslation(localizedLanguage = "${language}");

			DDLRecord.populateFieldText(
				ddlRecordFieldData = "${language} Parent",
				fieldName = "textParent"
			);

			DDLRecord.populateFieldText(
				ddlRecordFieldData = "${language} Child",
				fieldName = "textChild"
			);
		}

		for (var language : list "English (United States),Spanish (Spain),Portuguese (Brazil)") {
			DDLRecord.selectTranslation(localizedLanguage = "${language}");

			FormFields.viewTextValue(
				fieldName = "textParent",
				fieldValue = "${language} Parent"
			);

			FormFields.viewTextValue(
				fieldName = "textChild",
				fieldValue = "${language} Child"
			);
		}
	}
}