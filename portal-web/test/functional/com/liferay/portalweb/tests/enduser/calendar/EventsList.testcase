@component-name = "portal-calendar"
definition {
	property custom.properties = "default.layout.template.id=1_column${line.separator}company.default.time.zone=America/Los_Angeles";
	property osgi.app.includes = "events-display";
	property plugins.deployment.type = "osgi";
	property portal.release = "false";
	property portal.upstream = "true";
	property testray.main.component.name = "Calendar";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Page.add(pageName = "Calendar Page");

		Navigator.gotoPage(pageName = "Calendar Page");

		Portlet.addPG(pageName = "Calendar Page", portletName = "Calendar");

		Navigator.gotoPage(pageName = "Calendar Page");

		Portlet.addPG(pageName = "Calendar Page", portletName = "Events List");

		Navigator.gotoPage(pageName = "Calendar Page");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = "5"
	test ViewEventTime {
		property portal.release = "true";

		EventsListPortlet.viewDefaultMessage();

		CalendarNavigator.gotoAddEvent();

		PortletEntry.inputTitle(title = "Calendar Event Title");

		CalendarEvent.publish();

		Navigator.gotoPage(pageName = "Calendar Page");

		EventsListPortlet.viewHeader();

		EventsListPortlet.viewEventTime(eventTitle = "Calendar Event Title");

		EventsListPortlet.gotoEventToday(eventTitle = "Calendar Event Title");
	}

	@priority = "4"
	test ViewRepeatingEventLinks {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		CalendarNavigator.gotoAddEvent();

		PortletEntry.inputTitle(title = "Repeating Event Title");

		CalendarEvent.setRepeating(frequency = "Daily");

		CalendarEvent.setAllDay();

		CalendarEvent.publish();

		Navigator.gotoPage(pageName = "Calendar Page");

		EventsListPortlet.gotoEventToday(eventTitle = "Repeating Event Title");

		CalendarEvent.viewDetailsInPortlet(
			calendarName = "${siteName}",
			eventTitle = "Repeating Event Title"
		);

		var startDate = CalendarEvent.getEventStartDateViaDetails(startDate = "${startDate}");

		Navigator.gotoPage(pageName = "Calendar Page");

		EventsListPortlet.gotoEventUpcoming(eventTitle = "Repeating Event Title");

		CalendarEvent.viewDetailsInPortlet(
			calendarName = "${siteName}",
			eventTitle = "Repeating Event Title"
		);

		AssertTextNotEquals(
			locator1 = "CalendarViewEventDetails#START_DATE_FIELD",
			value1 = "${startDate}"
		);
	}
}