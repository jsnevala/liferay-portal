@component-name = "portal-staging"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Staging";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Page.add(pageName = "Staging Test Page", siteName = "Site Name");

		Navigator.openSiteURL(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging"
		);

		Staging.activateStagingCP(
			pageVersioningEnabled = "true",
			siteName = "Site Name"
		);

		Navigator.gotoStagedSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name"
		);
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();
		}
	}

	@priority = "4"
	test AddEventRepeatDaily {
		Staging.addEventRepeatDaily(
			scheduleTitleName = "Event Daily Name",
			siteName = "Site Name"
		);
	}

	@priority = "5"
	test AddPageVariation {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		Staging.addPageVariationPG(pageVariationName = "Page Variation Name");

		Navigator.gotoStagedSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name"
		);

		Staging.viewPageVariationPG(pageVariationName = "Page Variation Name");

		Navigator.gotoStagedSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name"
		);

		SitePageNavigator.gotoEditPage(page = "Staging Test Page");

		Page.addFriendlyURLLocalizedPG(
			friendlyUrlLocalized = "de_DE",
			localizedFriendlyURL = "/german"
		);

		Page.hideFromNavigationMenu();

		SitePageNavigator.gotoEditPage(page = "Staging Test Page");

		Page.viewPageFriendlyURLPG(friendlyUrlLocalized = "de_DE", pageName = "german");
	}

	@priority = "5"
	test AddPageWithPageVersioningEnabled {
		property portal.acceptance = "true";

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Page.add(
			pageName = "Staging Page Versioning",
			pageStaging = "true",
			siteName = "Site Name"
		);

		Navigator.gotoStagedSitePage(
			pageName = "Staging Page Versioning",
			siteName = "Site Name"
		);

		Portlet.addPG(portletName = "Hello World");

		Staging.markAsReadyForPublication();

		ProductMenu.gotoSite(site = "Site Name");

		Navigator.gotoStagedView();

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		Navigator.gotoStagedSitePage(
			pageName = "Staging Page Versioning",
			siteName = "Site Name"
		);

		Portlet.viewSpecificPG(portletName = "Hello World");
	}

	@priority = "5"
	test AddSitePagesVariation {
		property portal.acceptance = "true";

		Staging.addSitePagesVariationPG(
			sitePagesVariationName = "Site Pages Variation Name"
		);

		Navigator.gotoStagedSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name"
		);

		Staging.viewSitePagesVariationPG(
			sitePagesVariationName = "Site Pages Variation Name"
		);
	}
}