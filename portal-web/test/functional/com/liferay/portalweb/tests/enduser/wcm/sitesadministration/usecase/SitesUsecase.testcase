@component-name = "portal-wcm"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Site Administration";
	var globalSiteName = "Global";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.firstLoginPG();

			Site.tearDownCP();

			Page.tearDownPG();

			WebContent.tearDownCP(siteScopeName = "Global");

			WebContentTemplates.tearDownCP(siteScopeName = "Global");

			WebContentStructures.tearDownCP(siteScopeName = "Global");
		}
	}

	@priority = "5"
	test AddChildSiteFromImportedSiteTemplate {
		property test.name.skip.portal.instance = "SitesUsecase#AddChildSiteFromImportedSiteTemplate";
		property testray.component.names = "Training";

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations"
		);

		UsersAndOrganizationsNavigator.gotoOrganizations();

		LAR.importPortlet(
			larFileName = "organizations-7.0.lar",
			portletName = "Users and Organizations",
			uploadFrom = "dependencies"
		);

		UsersAndOrganizationsNavigator.gotoOrganizations();

		Organization.viewCP(
			orgName = "Space Program Academy of Continuing Education",
			orgType = "Organization"
		);

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations"
		);

		Organization.editOrganizationCreateSite(
			orgName = "College of Agricultural Science"
		);

		ProductMenu.gotoSite(site = "College of Agricultural Science");

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates"
		);

		LAR.importPortlet(
			larFileName = "class-site-template-7.0.lar",
			portletName = "Site Templates",
			uploadFrom = "dependencies"
		);

		SiteTemplates.viewCP(siteTemplateName = "Digital Class Room");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Site.addBlankCP(
			membershipType = "Private",
			parentSiteName = "College of Agricultural Science",
			siteName = "Moon Rocks 101",
			siteType = "Blank"
		);

		Site.selectPageType(
			selectPrivatePages = "true",
			siteTemplate = "Digital Class Room"
		);

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Site.addBlankCP(
			membershipType = "Private",
			parentSiteName = "College of Agricultural Science",
			siteName = "Moon Rocks 202",
			siteType = "Blank"
		);

		Site.selectPageType(
			checkPrivatePagesPropagation = "true",
			selectPrivatePages = "true",
			siteTemplate = "Digital Class Room"
		);

		Navigator.openPrivateSiteURL(siteName = "Moon Rocks 101");

		for (var pageName : list "Courses,Student Boards,Materials") {
			Navigator.gotoPrivateSitePage(
				pageName = "${pageName}",
				siteName = "Moon Rocks 101"
			);
		}

		Navigator.openPrivateSiteURL(siteName = "Moon Rocks 202");

		for (var pageName : list "Courses,Student Boards,Materials") {
			Navigator.gotoPrivateSitePage(
				pageName = "${pageName}",
				siteName = "Moon Rocks 202"
			);
		}
	}

	@priority = "3"
	test AddPageAndChildPageTranslations {
		property test.name.skip.portal.instance = "SitesUsecase#AddPageAndChildPageTranslations";
		var portalURL = PropsUtil.get("portal.url");

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Site.addBlankCP(siteName = "Site Name");

		SitePageNavigator.gotoPages(page = "Public Pages");

		Page.add(pageName = "Home");

		Page.addPageTranslations(
			page = "Home",
			pageTranslation = "es_ES",
			pageTranslationName = "Casa"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		Page.viewTranslationsPG(
			pageLocalizationMessage = "This page is displayed in Spanish (Spain). Display the page in English (United States). Set Spanish (Spain) as your preferred language.",
			pageLocalizationPreferredLanguage = "Set Spanish (Spain) as your preferred language.",
			pageName = "Casa",
			sitePageURL = "${portalURL}/es/web/site-name/home"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		Page.localizedPG(pageLocale = "Display the page in English (United States)");

		Page.addChild(
			page = "Public Pages",
			pageName = "Home",
			siteChildPageName = "Blog Page",
			siteName = "Site Name",
			siteScopeSelected = "true"
		);

		SitePageNavigator.gotoPages(page = "Public Pages");

		Page.addPageTranslations(
			page = "Blog Page",
			pageTranslation = "es_ES",
			pageTranslationName = "Blog Página"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		Page.viewTranslationsPG(
			childPageName = "Blog Página",
			pageLocalizationMessage = "This page is displayed in Spanish (Spain). Display the page in English (United States). Set Spanish (Spain) as your preferred language.",
			pageLocalizationPreferredLanguage = "Set Spanish (Spain) as your preferred language.",
			sitePageURL = "${portalURL}/es/web/site-name/blog-page"
		);
	}

	@priority = "3"
	test SitesTemplatePermissionPersistence {
		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Site.addCommunityCP(siteName = "Site Name");

		Navigator.openSiteURL(siteName = "Site Name");

		PermissionsInline.gotoConfigurationPG(portletName = "Polls Display");

		PermissionsInline.assertDefaultPG(
			roleList = "Guest,Owner,Power User,Site Member,User"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		PermissionsInline.gotoConfigurationPG(portletName = "Message Boards");

		PermissionsInline.assertDefaultPG(
			roleList = "Guest,Owner,Power User,Site Member,User"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		Portlet.addPG(portletName = "Asset Publisher");

		Navigator.openSiteURL(siteName = "Site Name");

		Portlet.viewTitle(portletName = "Asset Publisher");

		SiteTemplates.resetPageChanges(
			columnNumberTo = "1",
			portletName = "Asset Publisher"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		Portlet.viewNoSpecificPG(portletName = "Asset Publisher");

		PermissionsInline.gotoConfigurationPG(portletName = "Polls Display");

		PermissionsInline.assertDefaultPG(
			roleList = "Guest,Owner,Power User,Site Member,User"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		Polls.addPollPG(
			pollsQuestion = "What is the most important issue for this election?",
			pollsQuestionChoiceA = "The Economy",
			pollsQuestionChoiceB = "The National Debt",
			pollsQuestionTitle = "Election Poll"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		Polls.selectPollPG(pollsQuestionTitle = "Election Poll");

		Navigator.openSiteURL(siteName = "Site Name");

		Polls.viewPollPG(
			pollsQuestion = "What is the most important issue for this election?",
			pollsQuestionChoiceList = "The Economy,The National Debt"
		);

		PermissionsInline.gotoConfigurationPG(portletName = "Message Boards");

		PermissionsInline.assertDefaultPG(
			roleList = "Guest,Owner,Power User,Site Member,User"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		MessageboardsThread.addPG(
			threadBody = "This is the message boards thread.",
			threadSubject = "Thread"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		MessageboardsThread.gotoPG(
			lastPostUserName = "Test Test",
			postCount = "1",
			threadBody = "This is the message boards thread.",
			threadSubject = "Thread"
		);

		MessageboardsThread.viewPG(
			threadBody = "This is the message boards thread.",
			threadSubject = "Thread"
		);

		User.logoutPG();

		var portalURL = PropsUtil.get("portal.url");

		Navigator.openSpecificURL(url = "${portalURL}/web/site-name/home");

		Portlet.viewTitle(portletName = "Message Boards");

		MessageboardsThread.gotoPG(
			lastPostUserName = "Test Test",
			postCount = "1",
			threadBody = "This is the message boards thread.",
			threadSubject = "Thread"
		);

		MessageboardsThread.viewPG(
			threadBody = "This is the message boards thread.",
			threadSubject = "Thread"
		);

		Portlet.viewTitle(portletName = "Election Poll");

		AssertTextEquals(locator1 = "Message#INFO", value1 = "Please sign in to vote.");

		AssertElementPresent(locator1 = "Search#SEARCH_FIELD");

		Portlet.viewTitle(portletName = "User Statistics");

		SignIn.signIn(userEmailAddress = "test@liferay.com", userPassword = "test");

		Navigator.openSpecificURL(url = "${portalURL}/web/site-name/home");

		PermissionsInline.gotoConfigurationPG(portletName = "Polls Display");

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Remove",
			roleList = "Guest,Owner,Power User,Site Member,User"
		);

		Navigator.openSiteURL(siteName = "Site Name");

		PermissionsInline.gotoConfigurationPG(portletName = "Message Boards");

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Remove",
			roleList = "Guest,Owner,Power User,Site Member,User"
		);

		User.logoutPG();

		Navigator.openSpecificURL(url = "${portalURL}/web/site-name/home");

		AssertTextEquals(
			locator1 = "Message#ERROR_1_COLUMN_1",
			value1 = "You do not have the roles required to access this portlet."
		);

		AssertTextEquals(
			locator1 = "Message#ERROR_1_COLUMN_2",
			value1 = "You do not have the roles required to access this portlet."
		);
	}

	@priority = "4"
	test ViewDefaultGlobalScopeLandingPage {
		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites"
		);

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoSite(site = "Global");

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		Site.viewGlobalLandingDefault(portletName = "Web Content", siteName = "Global");
	}
}