@component-name = "portal-wcm"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Site Pages Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			WebContent.tearDownCP();

			Page.tearDownPG();
		}
	}

	@priority = "5"
	test AddPageFromExistingPage {
		property testray.component.names = "Training";

		Navigator.openURL();

		Page.add(pageName = "Site Page");

		Navigator.gotoPage(pageName = "Site Page");

		Page.add(pageName = "Site Page Name", pageTemplateName = "Content Display Page");

		Navigator.gotoPage(pageName = "Site Page Name");

		for (var portletName : list "Tags Navigation,Categories Navigation,Asset Publisher") {
			Portlet.viewSpecificPG(portletName = "${portletName}");
		}
	}

	@priority = "5"
	test DeletePages {
		Page.add(pageName = "Page1");

		Page.add(pageName = "Page2");

		Page.deletePG(pageName = "Page1");

		Page.viewDeletedPage(pageName = "Page1");

		Navigator.openURL();

		Page.deleteViaX(pageName = "Page2");

		Page.viewDeletedPage(pageName = "Page2");
	}

	@description = "This is a use case for LPS-71160"
	@priority = "5"
	test EditPageViaControlMenu {
		property testray.component.names = "Training";

		Page.add(pageName = "Site Page");

		Navigator.gotoEditPageViaControlMenu(pageName = "Site Page");

		Page.editName(pageNameEdit = "Site Page Edit");

		Page.view(pageName = "Site Page Edit");

		Navigator.gotoPage(pageName = "Site Page Edit");

		Navigator.gotoEditPageViaControlMenu(pageName = "Site Page Edit");

		Navigator.gotoSEOPanel();

		Page.editPageMetadata(
			HTMLTitle = "S.P.A.C.E.Home",
			metaTagsDescription = "Welcome to S.P.A.C.E., a graduate-level school for those passionate about expanding their horizons through science, research, and space travel",
			metaTagsKeywords = "space, education, space travel, graduateschool"
		);

		Navigator.gotoPage(pageName = "Site Page Edit");

		Navigator.gotoEditPageViaControlMenu(pageName = "Site Page Edit");

		Navigator.gotoSEOPanel();

		Page.viewPageMetadata(
			HTMLTitle = "S.P.A.C.E.Home",
			metaTagsDescription = "Welcome to S.P.A.C.E., a graduate-level school for those passionate about expanding their horizons through science, research, and space travel",
			metaTagsKeywords = "space, education, space travel, graduateschool"
		);
	}

	@priority = "4"
	test EditSitePage {
		Navigator.openURL();

		Page.add(pageName = "Site Page");

		SitePageNavigator.gotoEditPage(page = "Site Page");

		Page.editName(pageNameEdit = "Site Page Edit");

		Navigator.gotoPage(pageName = "Site Page Edit");

		Page.view(pageName = "Site Page Edit");
	}
}