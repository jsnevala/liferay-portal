@component-name = "portal-web-forms-and-data-lists"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Dynamic Data Lists";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name"
		);

		DynamicDataMapping.addField(
			field = "Image",
			fieldFieldLabel = "Image",
			fieldName = "image"
		);

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);

		WebContentNavigator.gotoManageTemplates();

		WebContentTemplates.addCP(
			templateDescription = "WC Template Description",
			templateLanguage = "ftl",
			templateName = "WC Template Name"
		);

		WebContentTemplates.addStructure(
			structureName = "WC Structure Name",
			templateFieldNames = "Image",
			templateLanguage = "ftl"
		);

		DDLDataDefinition.save();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Web Content"
		);
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test"
			);

			Site.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-69593."
	@priority = "4"
	test ViewFieldTranslations {
		property test.name.skip.portal.instance = "DDMStructureWithImageField#ViewFieldTranslations";

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(webContentTitle = "English (United States) Title");

		ItemSelector.gotoItemSelectorViaSelectButton();

		ItemSelector.uploadFile(
			navTab = "Documents and Media",
			uploadFileName = "Document_3.png"
		);

		FormFields.editTextAlt(
			fieldName = "image",
			fieldValue = "English (United States) Image Description"
		);

		for (var language : list "Spanish (Spain),Portuguese (Brazil)") {
			DDLRecord.addTranslation(localizedLanguage = "${language}");

			FormFields.editText(fieldName = "title", fieldValue = "${language} Title");

			ItemSelector.gotoItemSelectorViaSelectButton();

			ItemSelector.selectDMImage(imageFileName = "Document_3.png");

			FormFields.editTextAlt(
				fieldName = "image",
				fieldValue = "${language} Image Description"
			);
		}

		for (var language : list "English (United States),Spanish (Spain),Portuguese (Brazil)") {
			DDLRecord.selectTranslation(localizedLanguage = "${language}");

			FormFields.viewTextValue(fieldName = "title", fieldValue = "${language} Title");

			FormFields.viewTextValueAlt(
				fieldName = "image",
				fieldValue = "${language} Image Description"
			);
		}
	}
}